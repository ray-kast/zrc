#!/usr/bin/env zsh

set -e
setopt extendedglob

function yn() {
  local yn

  while :; do
    echo -n "$1" >&2

    read -k1 yn

    case $yn in
      y|Y)
        echo >&2
        echo y
        return 0
        ;;
      n|N)
        echo >&2
        echo n
        return 1
        ;;
      $'\n')
        [[ "$2" == 'y' ]] || return 1
        return 0
        ;;
      *)
        echo >&2
        ;;
    esac
  done
}

typeset -a del_files del_dirs
for arg in "${(@)@}"; do
  del_files=()
  del_dirs=()

  if [[ -f "$arg" ]]; then
    del_files+=("$arg")
  elif [[ -d "$arg" ]]; then
    del_dirs+=("$arg")
  else
    echo "ERROR: '$arg' is not a regular file or directory" >&2
    sleep 3
    continue
  fi

  for file in "$arg"/**/*(.DNon); do
    del_files+=("$file")
  done

  for dir in "$arg"/**/*(/DNon); do
    del_dirs+=("$dir")
  done

  echo "Files to delete ($#del_files):" >&2
  for file in "${(@)del_files}"; do
    echo " - $file" >&2
  done

  echo "...plus $#del_dirs dir(s)" >&2

  yn "Continue? [y/N] " n || continue

  for file in "${(@)del_files}"; do
    shred -u "$file"
  done

  for dir in "${(@)del_dirs}"; do
    rmdir -p --ignore-fail-on-non-empty "$dir"
  done

  if [[ -e "$arg" ]]; then
    echo "WARNING: '$arg' could not be deleted; manual action is required." >&2
    sleep 3
  fi
done
